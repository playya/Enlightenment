dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(evfs, 0.0.1)
AM_CONFIG_HEADER(config.h)

if test "$prefix" = "NONE"; then
  prefix=$ac_default_prefix
fi

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AM_PROG_LIBTOOL

AC_C_BIGENDIAN       
AC_STRUCT_ST_BLKSIZE
AC_STRUCT_ST_BLOCKS

use_threads="yes"

dnl Check for functions that we need to be thread-safe.
AC_CHECK_FUNCS(readdir_r ctime_r, , have_all_threadsafe_funcs="no")
if test "$have_all_threadsafe_funcs" = "no"; then
   use_threads="no"   
   AC_MSG_WARN(Not all functions needed for multithreaded build found, not using threads.)
fi

dnl Check for statfs call or other alternatives.
AC_CHECK_FUNC(statfs, , have_statfs="no")
if test "$have_statfs" = "no"; then
   AC_CHECK_FUNC(statvfs, have_statvfs="yes")
   if test "$have_stavtfs" = "yes"; then
      AC_DEFINE(HAVE_STATVFS, 1, [Build support for statfs])
   fi
else
   AC_DEFINE(HAVE_STATFS, 1, [Build support for statfs])
fi
  

#Thanks to gnome for the samba checking code
dnl ******************************
dnl Samba 3.0
dnl ******************************

AC_ARG_ENABLE(samba, [  --disable-samba       build without samba support])
msg_samba="no"
if test "x$enable_samba" != "xno"; then
  AC_ARG_WITH(samba-includes, [  --with-samba-includes=PREFIX     Location of samba includes.],
	      with_samba_includes="$withval", with_samba_includes="/usr/include")
  have_samba_includes="no"
  if test "x${with_samba_includes}" != "xno"; then
	CPPFLAGS_save="$CPPFLAGS"

	echo "before test, samba_includes: ${samba_includes}"

	CPPFLAGS="$CPPFLAGS -I$with_samba_includes"
	AC_CHECK_HEADER(libsmbclient.h, [ samba_includes="yes" ])
	CPPFLAGS="$CPPFLAGS_save"
	
	if test "x{samba_includes}" != "xno" -a "x${samba_includes}" != "x"; then
		have_samba_includes="yes"
		if test "${with_samba_includes}" != "/usr/include" ; then
			SAMBA_CFLAGS="-I$with_samba_includes"
		else
			SAMBA_CFLAGS=""
		fi
		
		CPPFLAGS="$CPPFLAGS -I$with_samba_includes"
		AC_CHECK_MEMBER(SMBCCTX.flags,
				[AC_DEFINE(HAVE_SAMBA_FLAGS,, [Defined if flags availible in SMBCCTXT])],,
				[#include <libsmbclient.h>])
		AC_CHECK_MEMBER(SMBCCTX.close,
				[AC_DEFINE(HAVE_SAMBA_OLD_CLOSE, , [Defined if old close is available in SMBCCTXT])],,
				[#include <libsmbclient.h>])
		CPPFLAGS="$CPPFLAGS_save"
	else
		SAMBA_CFLAGS=""
	fi
  fi
  echo "have_samba_includes: ${have_samba_includes}"
  AC_ARG_WITH(samba-libs, [  --with-samba-libs=PREFIX     	Location of Samba libs.],
	      with_samba_libs="$withval", with_samba_libs="/usr/lib")
  if test "x${with_samba_libs}" != "xno" -a "x${have_samba_includes}" != "xno"; then
	LDFLAGS_save="$LDFLAGS"
	
	LDFLAGS="$LDFLAGS -L$with_samba_libs"
	AC_CHECK_LIB(smbclient, smbc_new_context,samba_libs="yes", samba_libs="no")
	LDFLAGS="$LDFLAGS_save"
	if test "x${samba_libs}" != "xno"; then
		AC_DEFINE(HAVE_SAMBA,, [Define to 1 if you have the samba 3.0 libraries])
		msg_samba="yes"
                if test x$with_samba_libs != x/usr/lib; then
                        SAMBA_LIBS="-L$with_samba_libs -lsmbclient"
                else
                	SAMBA_LIBS="-lsmbclient"
                fi
	else
		SAMBA_CFLAGS=""
		SAMBA_LIBS=""
	fi
  fi
  AC_MSG_CHECKING(for Samba 3.0 libraries)
  AC_MSG_RESULT($msg_samba)
fi
AM_CONDITIONAL(HAVE_SAMBA, test $msg_samba = yes)
AC_SUBST(SAMBA_CFLAGS)
AC_SUBST(SAMBA_LIBS)


dnl Check for pthreads. Make sure we have both
dnl header and lib, otherwise, or when --disable-threads
dnl was given, build without thread support.
AC_ARG_ENABLE(threads,
	AC_HELP_STRING([--enable-threads],[enable thread support @<:@default=yes@:>@]),
	use_threads=$enableval)

dnl This needs revisiting
PTHREAD_CFLAGS=
PTHREAD_LIB=
if test "$use_threads" = "yes"; then
   AC_CHECK_HEADER(pthread.h, pthread_header_ok="yes", pthread_header_ok="no")
   if test "$pthread_header_ok" = "yes"; then
      AC_CHECK_LIB(pthread, main, PTHREAD_LIB="-lpthread", PTHREAD_LIB="error")
      if test "$PTHREAD_LIB" = "error"; then
         AC_CHECK_LIB(c_r, main, PTHREAD_LIB="-pthread", pthread_lib_ok="no")
      fi
   fi
   if test "$pthread_header_ok" = "no" -o "$pthread_lib_ok" = "no"; then
      cat <<EOF;
------------------------------------------------------
WARNING: Could not find a complete POSIX threads
(pthreads) installation on your system -- building
without thread support.
------------------------------------------------------
EOF
      AM_CONDITIONAL(USE_THREADS_FILES, false)
   else
      AC_DEFINE(USE_THREADS, 1, [Build support for threads])
      AM_CONDITIONAL(USE_THREADS_FILES, true)
      if test "$PTHREAD_LIB" = "-pthread"; then
         PTHREAD_CFLAGS="-D_THREAD_SAFE -D_REENTRANT -D_POSIX_PTHREAD_SEMANTICS"
      else
         PTHREAD_CFLAGS="-D_REENTRANT -D_POSIX_PTHREAD_SEMANTICS"
      fi
      CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
      LIBS="$LIBS $PTHREAD_LIB"
   fi
else
   AM_CONDITIONAL(USE_THREADS_FILES, false)
fi

AC_SUBST(PTHREAD_CFLAGS)
AC_SUBST(PTHREAD_LIB)

dnl Check for libxml.
AC_ARG_WITH(xml2,
        AC_HELP_STRING([--with-xml2=DIR],[use libxml2 found in DIR]),
        [CFLAGS="$CFLAGS -I$withval/include"
         LIBS="-L$withval/lib $LIBS"])

AC_PATH_GENERIC(xml2, 2.3.10, [
    AC_SUBST(xml2_libs)
    AC_SUBST(xml2_cflags)],
    AC_MSG_ERROR(Cannot find libxml2: Is xml2-config in path?))
xml2_libs=`xml2-config --libs`
xml2_cflags=`xml2-config --cflags`


dnl Checking for Perl:
AC_PATH_PROG(PERL,perl,0)
AC_SUBST(PERL)

PROG="ecore-config";
AC_PATH_PROG(ECORE_CONFIG, $PROG, "", $PATH)
if test -z "$ECORE_CONFIG" ; then
  echo $PROG " is not in your \$PATH. Please ensure it is.";
  echo "Read the manual page for you shell as to how to extend your path.";
  AC_MSG_ERROR(Cannot find $PROG)
fi
ecore_cflags=`$ECORE_CONFIG --cflags`
ecore_libs=`$ECORE_CONFIG --libs`
AC_SUBST(ecore_cflags)
AC_SUBST(ecore_libs)




dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale", "Locale directory")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale", "Locale directory")
fi


dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}", "Data directory")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}", "Data directory")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}", "Data directory")
fi

dnl Set the package lib dir
if test "x${libdir}" = 'x${prefix}/lib'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_PLUGIN_DIR, "${ac_default_prefix}/lib/${PACKAGE}", "Plugin directory")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_PLUGIN_DIR, "${prefix}/lib/${PACKAGE}", "Plugin directory")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_PLUGIN_DIR, "${prefix}/lib/${PACKAGE}", "Plugin directory")
fi



dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}", "Source directory")


dnl Backtrace generation code, based on code from Eterm. Cheers Michael :)
AC_PATH_PROG(DBX, dbx, no)
if test "$DBX" != "no"; then
  AC_DEFINE_UNQUOTED(DBX, "$DBX", "dbx program")
fi
AC_PATH_PROG(GDB, gdb, no)
if test "$GDB" != "no"; then
  AC_DEFINE_UNQUOTED(GDB, "$GDB", "gdb program")
fi
AC_PATH_PROG(PSTACK, pstack, no, $PATH:/usr/proc/bin:/usr/sbin)
if test "$PSTACK" != "no"; then
  AC_DEFINE_UNQUOTED(PSTACK, "$PSTACK", "pstack program")
fi
AC_CHECK_LIB(cl, U_STACK_TRACE, LIBS="$LIBS -lcl")

if test "$GDB" != "no"; then
  GDB_CMD_FILE=`eval eval eval eval echo "$datadir/$PACKAGE/gdb.scr"`
  AC_DEFINE_UNQUOTED(GDB_CMD_FILE, "$GDB_CMD_FILE", "gdb command file")
else
  GDB_CMD_FILE=""
fi
AC_SUBST(GDB_CMD_FILE)


dnl Look for jade for sgml translations.
AC_ARG_WITH(dbsheets,
       [  --with-dbsheets=DIR     use DIR to specify your DocBook stylesheets installation path.],
       DB_STYLESHEETS="$withval", DB_STYLESHEETS="/usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh")
AC_SUBST(DB_STYLESHEETS)
AC_PATH_PROG(JADE, jade)
AM_CONDITIONAL(HAVE_JADE, test "x$JADE" != "x" && test -d "$DB_STYLESHEETS")



dnl Various conditionals
AM_CONDITIONAL(BUILD_ECORE_DEPENDENT,      test "$ecore" != "no")
AC_DEFINE(HAVE_ECORE, 1, [Build with ecore])


AC_OUTPUT([
Makefile
src/Makefile
src/bin/Makefile
src/lib/Makefile
src/plugins/Makefile
src/demo/Makefile
src/include/Makefile
evfs-config
], [
chmod +x evfs-config
])
