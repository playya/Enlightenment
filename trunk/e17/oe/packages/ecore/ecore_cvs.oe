DESCRIPTION = "Ecore is the core event abstraction layer for the enlightenment \
foundation libraries. It makes makes doing selections, drag and drop, event loops, \
timeouts and idle handlers fast, optimized, and convenient."
HOMEPAGE = "http://www.enlightenment.org"
MAINTAINER = "That Crazy fool emte <emte@labotomy.net>"
SECTION = "e/libs"
PRIORITY = "optional"
LEAD_SONAME = "libecore.so"
PV = "${CVSDATE}"
DEPENDS = "eet evas libiconv"
PR = "r0"

SRC_URI = "cvs://anonymous@cvs.sourceforge.net/cvsroot/enlightenment;module=e17/libs/ecore"
S = "${WORKDIR}/ecore"

inherit autotools binconfig

export EET_CONFIG = "${STAGING_BINDIR}/eet-config"
export EVAS_CONFIG = "${STAGING_BINDIR}/evas-config"
export FREETYPE_CONFIG = "${STAGING_BINDIR}/freetype-config"

EXTRA_OECONF = "--enable-ecore-fb \
		--enable-ecore-job \
                --enable-ecore-evas \
		--enable-ecore-evas-fb \
		--enable-ecore-evas-x \
		--disable-ecore-evas-gl \
		--enable-ecore-con \
		--enable-ecore-ipc \
		--enable-ecore-txt \
		--enable-ecore-x \
		--enable-ecore-config \
		--disable-openssl \
		--with-iconv-link=-liconv"

parts = "Ecore Ecore_Job \
	 Ecore_Txt Ecore_Fb Ecore_Con \
	 Ecore_Ipc Ecore_Evas Ecore_Config \
         Ecore_X"

do_stage () {
	for p in ${parts}; do
		dir=`echo $p|tr A-Z a-z`
		install -m 0644 ${S}/src/lib/$dir/$p.h ${STAGING_INCDIR}/
		oe_libinstall -C src/lib/$dir lib$dir ${STAGING_LIBDIR}/
	done
	install -m 0644 ${S}/src/lib/ecore/Ecore_Data.h ${STAGING_INCDIR}/
	install -m 0644 ${S}/ecore.m4 ${STAGING_DATADIR}/aclocal/
}

PACKAGES += "ecore-examples"

FILES_${PN} = "${libdir}/libecore*.so* ${libdir}/ecore_config_ipc_ecore.so"
FILES_${PN}-dev += "${bindir}/ecore-config ${libdir}/pkgconfig"
FILES_${PN}-examples = "${bindir}/ecore_test ${bindir}/ecore_evas_test ${datadir}"
