dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(epsilon, 0.3.0.010)
AM_CONFIG_HEADER(src/config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_C_BIGENDIAN
AC_C_CONST
AM_ENABLE_SHARED
AM_PROG_LIBTOOL

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall " ;;
  esac
fi
changequote([,])dnl

PKG_CHECK_MODULES(IMLIB2, [imlib2 >= 1.0.0],
  [ have_imlib2="yes" ],
  [
    have_imlib2="no"
    AC_MSG_ERROR(Cannot find imlib2: Is imlib2-config in the path?)
  ]
)

dnl Make sure the epeg header/lib is available
PKG_CHECK_MODULES(EPEG, [epeg >= 0.9.0],
  [
    AC_DEFINE(HAVE_EPEG_H, 1, [Epeg.h is available])
    have_epeg="yes"
  ],
  [
    have_epeg="no"
    AC_MSG_WARN(Cannot find Epeg on your system: jpeg thumbnail suckage!)
  ]
)

dnl Make sure the png header/lib is available
PKG_CHECK_MODULES(PNG, libpng12 >= 1.2.0,
  [
    have_png="yes"
    AC_DEFINE(HAVE_PNG_H, 1, [png.h is available])
  ],
  [
    have_png="no"
    AC_MSG_WARN(Cannot find libpng on your system: png thumbnail suckage!)
  ]
)

AC_PATH_PROG(PERL, perl, 0)
AC_SUBST(PERL)

PKG_CHECK_MODULES(EVAS, [evas >= 0.9.9])

PKG_CHECK_MODULES(ECORE, [
    ecore-evas >= 0.9.9
    ecore-file >= 0.9.9
    ecore-ipc >= 0.9.9
  ]
)

epdf=""
PKG_CHECK_MODULES(EDJE, [edje >= 0.5.0])
PKG_CHECK_MODULES(EPDF, [epdf >= 0.1], epdf="yes",epdf="no")
AM_CONDITIONAL(HAVE_EPDF, test $epdf = "yes")

xine=""
AC_MSG_CHECKING(whether the xine thumbnailer was explicitly disabled)
AC_ARG_ENABLE(thumbnailer-xine, AC_HELP_STRING([--enable-thumbnailer-xine],[build the thumbnailer for videos]),
        [ ac_use_plugin_xine="$enableval" ],
        [ ac_use_plugin_xine="yes" ]
        )
if test X"$ac_use_plugin_xine" = Xyes; then
        AC_MSG_RESULT(no)
        PKG_CHECK_MODULES(XINE, [libxine >= 1.0.0],
          [
            AC_DEFINE(HAVE_LIBXINE, 1, build xinefile thumbnailer)
	    xine="xine"
	  ],
          [
            AC_MSG_WARN([libxine too old; no thumbnails for videos...])
          ]
        )
else
        AC_MSG_RESULT(yes)
        xine=""
fi
AM_CONDITIONAL(HAVE_XINE, test $xine = "xine")

requirements="imlib2 evas ecore-evas ecore-file ecore-con edje"
AC_SUBST(requirements)

#####################################################################
## Output

AC_OUTPUT([
Makefile
epsilon.pc
epsilon.spec
src/Makefile
src/include/Makefile
src/lib/Makefile
src/lib/exiftags/Makefile
src/bin/Makefile
src/plugins/Makefile
debian/changelog
])

echo
echo "Finished configuring $PACKAGE $VERSION"
echo
echo "Support for freedesktop png thumbnailing .... $have_png"
echo "Support for epeg thumbnail extension     .... $have_epeg"
echo
echo "Now type 'make' (gmake on some systems) to compile $PACKAGE"
echo

