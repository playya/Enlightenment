dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AM_INIT_AUTOMAKE(ewl, 0.0.4.004)
AM_CONFIG_HEADER(ewl-config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_C_CONST
AM_ENABLE_SHARED
AM_PROG_LIBTOOL
AC_C___ATTRIBUTE__

dnl Check for Solaris or other OS
case "$host" in
     *-sun-*)
        AC_DEFINE(HAVE_SUN, 1, [Support for SunOS/Solaris])
        ;;
     *) ;;
esac

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale", [Locale data directory])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale", [Locale data directory])
fi

dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}", [Shared data directory])
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}", [Shared data directory])
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}", [Shared data directory])
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}", [Source code directory])

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

AC_PATH_GENERIC(evas, 0.9.9,
  [ ],
  [
    echo "ERROR:"
    echo "The evas-config development script was not found in your execute"
    echo "path. This may mean one of several things"
    echo "1. You may not have installed the evas-devel (or evas-dev)"
    echo "   packages."
    echo "2. You may have evas installed somewhere not covered by your path."
    echo ""
    echo "If this is the case make sure you have the packages installed, AND"
    echo "that the evas-config script is in your execute path (see your"
    echo "shell's manual page on setting the \$PATH environment variable), OR"
    echo "alternatively, specify the script to use with --with-evas-config."
    AC_MSG_ERROR([Fatal Error: no evas-config detected.])
  ]
)

PCFLAGS=$CFLAGS
CFLAGS=$EVAS_CFLAGS" "$CFLAGS
AC_CHECK_HEADERS(Evas_Engine_Software_X11.h, [], [], [
	#include <Evas.h>
	])

AC_CHECK_HEADERS(Evas_Engine_GL_X11.h, [], [], [
	#include <Evas.h>
	])

AC_CHECK_HEADERS(Evas_Engine_FB.h, [], [], [
	#include <Evas.h>
	])

AC_PATH_GENERIC(ecore, 0.9.9,
  [ ],
  [
    echo "ERROR:"
    echo "The ecore-config development script was not found in your execute"
    echo "path. This may mean one of several things"
    echo "1. You may not have installed the ecore-devel (or ecore-dev)"
    echo "   packages."
    echo "2. You may have ecore installed somewhere not covered by your path."
    echo ""
    echo "If this is the case make sure you have the packages installed, AND"
    echo "that the ecore-config script is in your execute path (see your"
    echo "shell's manual page on setting the \$PATH environment variable), OR"
    echo "alternatively, specify the script to use with --with-ecore-config."
    AC_MSG_ERROR([Fatal Error: no ecore-config detected.])
  ]
)

CFLAGS=$ECORE_CFLAGS" "$CFLAGS
AC_PATH_GENERIC(edje, 0.5.0,
  [ ],
  [
    echo "ERROR:"
    echo "The edje-config development script was not found in your execute"
    echo "path. This may mean one of several things"
    echo "1. You may not have installed the edje-devel (or edje-dev)"
    echo "   packages."
    echo "2. You may have edje installed somewhere not covered by your path."
    echo ""
    echo "If this is the case make sure you have the packages installed, AND"
    echo "that the edje-config script is in your execute path (see your"
    echo "shell's manual page on setting the \$PATH environment variable), OR"
    echo "alternatively, specify the script to use with --with-edje-config."
    AC_MSG_ERROR([Fatal Error: no edje-config detected.])
  ]
)

requirements="evas ecore edje"

CFLAGS=$EDJE_CFLAGS" "$CFLAGS
AC_PATH_GENERIC(emotion, 0.0.1,
  [ 
    have_emotion="yes"
    ewl_media_include="#include <ewl_media.h>"
    requirements="$requirements emotion"
  ],
  [
    have_emotion="no"
    echo "No emotion-config detected."
  ]
)
AM_CONDITIONAL(BUILD_EMOTION_SUPPORT, test "x$have_emotion" = "xyes")

CFLAGS=$EMOTION_CFLAGS" "$CFLAGS

###

CFLAGS=$PCFLAGS

AC_SUBST(ewl_media_include)

AC_SUBST(requirements)

AC_OUTPUT([
Makefile
ewl.spec
ewl.pc
ewl-config
src/Makefile
src/bin/Makefile
src/lib/Ewl.h
src/lib/Makefile
data/Makefile
data/themes/Makefile
], [
chmod +x ewl-config
]
)
