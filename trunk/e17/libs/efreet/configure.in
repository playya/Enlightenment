AC_INIT(configure.in)
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AM_INIT_AUTOMAKE(efreet, 0.0.3)
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_C_CONST
AM_ENABLE_SHARED
AM_PROG_LIBTOOL
AC_C___ATTRIBUTE__

dnl Set PACKAGE_DATA_DIR in config.h.
AC_DEFINE_DIR([PACKAGE_DATA_DIR], [datadir], [Shared Data Directory.])

PCFLAGS=$CFLAGS

PKG_CHECK_MODULES(ECORE, [ecore >= 0.9.9 ecore-file >= 0.9.9])

CFLAGS=$ECORE_CFLAGS" "$CFLAGS

requirements="ecore ecore-file"

AC_ARG_ENABLE(ecore-desktop,
	[AC_HELP_STRING([--enable-ecore-desktop],[Enable Ecore_Desktop tests])],
	[enable_ecore_desktop=$enableval], [enable_ecore_desktop="auto"])

have_ecore_desktop=no

if test "x$enable_ecore_desktop" != "xno" ; then
  PKG_CHECK_MODULES(ECORE_DESKTOP, [ecore-desktop >= 0.9.9], [
    have_ecore_desktop=yes
  ], [
    have_ecore_desktop=no
  ])
fi

if test "x$have_ecore_desktop" = "xyes" ; then
  AC_DEFINE(ENABLE_ECORE_DESKTOP, 1, [Enable Ecore_Desktop])
elif test "x$enable_ecore_desktop" = "xyes" ; then
  AC_MSG_ERROR(No Ecore_Desktop found disabling comparison tests.)
fi
AM_CONDITIONAL(HAVE_ECORE_DESKTOP, test "x$have_ecore_desktop" = xyes)

AC_ARG_ENABLE(strict-spec,
	[AC_HELP_STRING([--enable-strict-spec],[Enable strict spec compliance])],
	[enable_strict_spec=$enableval], [enable_strict_spec="auto"])
STRICT_SPEC=0
if test "x$enable_strict_spec" = "xyes" ; then
    STRICT_SPEC=1
fi
AC_DEFINE_UNQUOTED(STRICT_SPEC, $STRICT_SPEC, [Strict Spec Compliance])

CFLAGS=$PCFLAGS

AC_SUBST(requirements)
AC_OUTPUT([
efreet-config
efreet.pc
Makefile
src/Makefile
src/lib/Makefile
src/bin/Makefile
src/bin/data/Makefile
src/bin/data/sub/Makefile
src/bin/compare/Makefile
], []
)
