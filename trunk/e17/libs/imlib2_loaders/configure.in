dnl Process this file with autoconf to create configure.

AC_INIT(src/loader_xcf.c)
AM_INIT_AUTOMAKE(imlib2_loaders, 0.0.1)
AM_CONFIG_HEADER(config.h)

AC_C_BIGENDIAN	      

AC_PROG_CC
AM_PROG_CC_STDC
AC_C_CONST

AC_PROG_INSTALL
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

AC_PATH_X
AC_PATH_XTRA

AC_ARG_ENABLE(edb,
[  --disable-edb           disable building the edb loader],
[
   if test x$enableval = xno; then
      edb=no;
      EDB_CONFIG_IN_PATH=disabled;
   fi
])

AC_ARG_ENABLE(xcf,
[  --disable-xcf           disable building the xcf loader],
[
   if test x$enableval = xno; then
      xcf=no;
   fi
])

AC_ARG_WITH(imlib2,
[  --with-imlib2=DIR       use DIR where imlib2 was installed (eg /opt/imlib2)],
[
  imlib2_includes="-I$withval/include";
  imlib2_libs="$LDFLAGS -L$withval/lib";
  #Don't use imlib2-config if the path is given
  IMLIB2_CONFIG_IN_PATH=ignored
  AC_MSG_CHECKING([for imlib2-config])
  AC_MSG_RESULT(ignored)
], 
[
  AC_CHECK_PROG(IMLIB2_CONFIG_IN_PATH, imlib2-config, yes, no)
  if test $IMLIB2_CONFIG_IN_PATH = no; then
    echo "ERROR:"
    echo "The imlib2-config development script was not found in your execute"
    echo "path. This may mean one of several things"
    echo "1. You may not have installed the Imlib2-devel (or Imlib2-dev)"
    echo "   packages."
    echo "2. You may have Imlib2 installed somewhere not covered by your path."
    echo ""
    echo "If this is the case make sure you have the packages installed, AND"
    echo "that the imlib2-config script is in your execute path (see your"
    echo "shell's manual page on setting the \$PATH environment variable)."
    AC_MSG_ERROR([Fatal Error: no imlib2-config detected.])
    exit;
  fi
])  

if test x$edb != xno; then
  AC_ARG_WITH(edb,
  [  --with-edb=DIR          use DIR where edb was installed (eg /opt/edb)],
  [
      edb_includes="-I$withval/include";
      edb_libs="-L$withval/lib";
      #Don't use edb-config if the path is given
      EDB_CONFIG_IN_PATH=ignored
      AC_MSG_CHECKING([for edb-config])
      AC_MSG_RESULT(ignored)
  ],
  [
     AC_CHECK_PROG(EDB_CONFIG_IN_PATH, edb-config, yes, no)
     if test $EDB_CONFIG_IN_PATH = no; then
       echo "WARNING:"
       echo "------------------------------------------------------------------"
       echo "The edb-config development script was not found in your execute"
       echo "path. This may mean one of several things"
       echo "1. You may not have installed the edb-devel (or edb-dev)"
       echo "   packages."
       echo "2. You may have edb installed somewhere not covered by your path."
       echo ""
       echo "If this is the case make sure you have the packages installed, AND"
       echo "that the edb-config script is in your execute path (see your"
       echo "shell's manual page on setting the \$PATH environment variable)."
       echo "------------------------------------------------------------------"
       AC_MSG_WARN([no edb-config detected.])
     fi
  ])
else
  AC_MSG_CHECKING([for edb-config])
  AC_MSG_RESULT(disabled)
fi

AM_CONDITIONAL(EDB_LOADER, 
            test \( $EDB_CONFIG_IN_PATH = yes -o \
                    $EDB_CONFIG_IN_PATH = ignored \) -a x$edb != xno )

AM_CONDITIONAL(XCF_LOADER, test x$xcf != xno )

if test $IMLIB2_CONFIG_IN_PATH = yes; then
  imlib2_includes=`imlib2-config --cflags`
  imlib2_libs=`imlib2-config --libs`
fi

if test $EDB_CONFIG_IN_PATH = yes -a x$edb != xno; then
   edb_includes=`edb-config --cflags`
   edb_libs=`edb-config --libs`
fi

AC_SUBST(imlib2_includes)
AC_SUBST(imlib2_libs)
AC_SUBST(edb_includes)
AC_SUBST(edb_libs)

AC_OUTPUT([
Makefile
src/Makefile
debian/Makefile
imlib2_loaders.spec
])

echo    "\nBuild summary:"
echo    "------------------------"
echo -n " XCF: "
if test x$XCF_LOADER_TRUE = "x#"; then
   echo    "Disabled "
else
   echo    "Enabled "
fi   

echo -n " Edb: "
if test x$EDB_LOADER_TRUE = "x#"; then
   echo    "Disabled "
else
   echo    "Enabled "
fi   

echo    "------------------------\n"
